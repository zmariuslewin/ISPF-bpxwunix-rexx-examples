/* rexx */ /* Show Model and Architecture level at top right of screen */       
                                                                                
trace o                                                                         
/* Example of use:                                                     */       
/* Command ===> tso arch                                               */       
/* Result, top right of screen: 'Architecture: ARCH(13)'               */       
/*                                                                     */       
/* The ARCH compilation parameter is used by:                          */       
/* a) COBOL starting from V5.                                          */       
/* b) The C compiler (with TUNE).                                      */       
/* c) ABO (Automatic Binary Optimization).                             */       
                                                                                
/*                                                                              
For correspondance, confer:                                                     
1) ARCH compilation parameter in latest version of the COBOL                    
   Programming Guide and                                                        
2) Google search on: ARCHopt.html                                               
Hardware Models (as of 2020):                                                   
                                                                                
ARCH(7)  2094-xxx models (IBM System z9 EC)                                     
         2096-xxx models (IBM System z9 BC)                                     
ARCH(8)  2097-xxx models (IBM System z10 EC)                                    
         2098-xxx models (IBM System z10 BC)                                    
ARCH(9)  2817-xxx models (IBM zEnterprise z196 EC)                              
         2818-xxx models (IBM zEnterprise z114 BC)                              
ARCH(10) 2827-xxx models (IBM zEnterprise EC12)                                 
         2828-xxx models (IBM zEnterprise BC12)                                 
ARCH(11) 2964-xxx models (IBM z13)                                              
         2965-xxx models (IBM z13s)                                             
ARCH(12) 3906-xxx models (IBM z14)                                              
         3907-xxx (IBM z14 ZR1) models                                          
ARCH(13) 8561-xxx models (IBM z15)                                              
         8562-xxx models                                                        
*/                                                                              
                                                                                
/*_____________________________________________________________________*/       
/*                     TABLE TO UPDATE                                 */       
/* Update frequency estimate: every two years.                         */       
                                                                                
table = '2094 ARCH(7)     2096 ARCH(7)',      /* COBOL V6.2 default    */       
        '2097 ARCH(8)     2098 ARCH(8)',                                        
        '2817 ARCH(9)     2818 ARCH()',                                         
        '2827 ARCH(10)    2828 ARCH(10)',                                       
        '2964 ARCH(11)    2965 ARCH(11)',     /* Z13                   */       
        '3906 ARCH(12)    3907 ARCH(12)',     /* Z14                   */       
        '8561 ARCH(13)    8562 ARCH(13)',     /* Z15                   */       
        '9996 DUMMY       9997 DUMMY   ',     /* spare1                */       
        '9998 DUMMY       9999 DUMMY   '      /* spare2                */       
/*                                                                     */       
/* Rule of thumb: On zx, ARCH = x - 2                                  */       
/*_____________________________________________________________________*/       
                                                                                
  /* Issue z/OS command: D M=CPU                                       */       
  /* to obtain model then architecture level from correspondance       */       
  /* table.                                                            */       
                                                                                
  /* Alternative 1:                                                    */       
  /* call bpxwunix "uname -m",,out. ; model = out.1                    */       
                                                                                
  /* Alternative 2:                                                    */       
  /* address syscall 'uname u.'; model = u.5                           */       
                                                                                
  /* Alternative 3:                                                    */       
  /* if syscpus('cpus.') = 0 then                                      */       
  /*    model = right(word(cpus.1,words(cpus.1)),4)                    */       
  /*    model made of four rightmost characters of last word of cpus.1 */       
                                                                                
  /* Also confer IPCS subcommand CBFORMAT CVT and field MDL at         */       
  /* CVT - 0006                                                        */       
  /* and line 6 in userid.ISPVCALL.TRACE                               */       
                                                                                
  cmd = 'd m=cpu'                                                               
  call execute_MVS_command(cmd) /* result should be ARCH(xx)           */       
                                                                                
  address ispexec                                                               
    "control errors return"                                                     
                                                                                
   /* ------------------------------------- *                                   
    | Build zedsmsg and zedlmsg             |                                   
    | - zedsmsg in shared pool, 24 char max |                                   
    | - zedlmsg in shared pool, 79 char max |                                   
    * ------------------------------------- */                                  
                                                                                
    zedsmsg = "Model: "model result                                             
    zedlmsg = 'Model: 'model'. The Architecture Level is' result'.'             
    "setmsg msg(isrz000)"                                                       
                                                                                
  address /* from ispexec                                              */       
exit                                                                            
/*________________________ROUTINE FOLLOWS ____________________________________*/
                                                                                
execute_MVS_command: procedure expose table model                               
parse arg cmd                                                                   
/* It returns ARCH(xx) or error message.                                      */
                                                                                
drop c.                                                                         
c.0 = 1                                                                         
c.1 = left(cmd,80)                                                              
                                                                                
/* Execute command.                                                           */
/* Result in isfulog. stem                                                    */
                                                                                
rc = isfcalls("on")                    /* access = on                         */
isfcons = "sdsf"random()               /* console = sdsfxxx                   */
isfdelay = 1 /* Wait 1 second before retrieving results                       */
address sdsf isfslash "("c.") (wait)"                                           
isfslash_rc = rc                       /* save return code                    */
rc = isfcalls("off")                   /* access = off                        */
                                                                                
/* If ok, display result.                                                     */
                                                                                
if isfslash_rc = 0 then do                                                      
     res = 'ARCH: Unknown'             /* initialize res (result)             */
     do i = 1 to isfulog.0                                                      
                                                                                
        p = pos('CPC SI',isfulog.i) /* result of STSI command                 */
        if p >  0 then do /* table search with wordpos                        */
           parse var isfulog.i . '=' model '.' . /* extract model number      */
           model = strip(model)                  /* remove leading space      */
           q = wordpos(strip(model,'L','0'),table)                              
           if q > 0 then do /* model found in table, ARCH is next word        */
                    res = word(table,q+1) /* architecture level e.g. ARCH(13) */
           end /* model found in rexx table                                   */
           else do  /* model not found in rexx table                          */
                parse source . . rexxname .                                     
                say "Model found "model" not in rexx "rexxname" table."         
                say "Please consider updating this rexx."                       
           end /* model not found in rexx table                               */
           leave         /* exit loop                                         */
        end /* if p > 0       line contains 'CPC SI'                          */
                                                                                
     end /* do i = 1 to isfulog.0                                             */
                                                                                
end /* if isfslash_rc = 0                                                     */
                                                                                
return res                                                                      
/*____________________________________________________________________________*/
